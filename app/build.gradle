apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            storeFile file('keystore')
            storePassword "${keystore_password}"
            keyAlias 'trakt'
            keyPassword "${key_password}"
        }
    }

    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    defaultConfig {
        applicationId "com.unbounds.trakt"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            def properties = new Properties()
            properties.load(new FileInputStream(rootProject.file("trakt.properties")))
            buildConfigField("String", "CLIENT_ID", "\"${properties.client_id}\"")
            buildConfigField("String", "CLIENT_SECRET", "\"${properties.client_secret}\"")
            buildConfigField("String", "BASE_API_URL", "\"https://api-v2launch.trakt.tv\"")
            buildConfigField("String", "BASE_URL", "\"https://trakt.tv\"")

            resValue "string", "oauth_referrer_scheme", '"unbounds-trakt"'
            resValue "string", "oauth_referrer", '"unbounds-trakt://oauth"'
        }

        debug {
            applicationIdSuffix '.debug'
            def properties = new Properties()
            properties.load(new FileInputStream(rootProject.file("trakt_staging.properties")))
            buildConfigField("String", "CLIENT_ID", "\"${properties.client_id}\"")
            buildConfigField("String", "CLIENT_SECRET", "\"${properties.client_secret}\"")
            buildConfigField("String", "BASE_API_URL", "\"http://api.staging.trakt.tv\"")
            buildConfigField("String", "BASE_URL", "\"http://staging.trakt.tv\"")

            resValue "string", "oauth_referrer_scheme", '"unbounds-trakt-debug"'
            resValue "string", "oauth_referrer", '"unbounds-trakt-debug://oauth"'
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:cardview-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.0.15'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    testCompile 'junit:junit:4.12'
}
