apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            if (project.hasProperty("keystore_password") && project.hasProperty("key_password")) {
                storeFile file('keystore')
                storePassword "${keystore_password}"
                keyAlias 'trakt'
                keyPassword "${key_password}"
            }
        }
    }

    compileSdkVersion 29
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.unbounds.trakt"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        def properties = new Properties()
        properties.load(new FileInputStream(rootProject.file("trakt.properties")))

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            buildConfigField("String", "CLIENT_ID", "\"${properties.client_id}\"")
            buildConfigField("String", "CLIENT_SECRET", "\"${properties.client_secret}\"")
            buildConfigField("String", "BASE_API_URL", "\"https://api-v2launch.trakt.tv\"")
            buildConfigField("String", "BASE_URL", "\"https://trakt.tv\"")

            resValue "string", "oauth_referrer_scheme", '"unbounds-trakt"'
            resValue "string", "oauth_referrer", '"unbounds-trakt://oauth"'
        }

        debug {
            applicationIdSuffix '.debug'
            buildConfigField("String", "CLIENT_ID", "\"${properties.staging_client_id}\"")
            buildConfigField("String", "CLIENT_SECRET", "\"${properties.staging_client_secret}\"")
            buildConfigField("String", "BASE_API_URL", "\"http://api.staging.trakt.tv\"")
            buildConfigField("String", "BASE_URL", "\"http://staging.trakt.tv\"")

            resValue "string", "oauth_referrer_scheme", '"unbounds-trakt-debug"'
            resValue "string", "oauth_referrer", '"unbounds-trakt-debug://oauth"'
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.browser:browser:1.2.0'

    implementation 'com.google.android.material:material:1.2.0'

    implementation 'io.reactivex:rxandroid:1.0.1'
    implementation 'io.reactivex:rxjava:1.0.15'

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.picasso:picasso:2.71828'

    testImplementation 'junit:junit:4.13'
}
